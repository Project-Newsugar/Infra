# 1. ECR Repository ìƒì„± (Frontend, Backend)
resource "aws_ecr_repository" "repos" {
  for_each             = toset(var.ecr_repo_names)
  name                 = each.value
  image_tag_mutability = "MUTABLE"

force_delete = true

  image_scanning_configuration {
    scan_on_push = true
  }

  tags = { Name = each.value }
}

# ìˆ˜ëª… ì£¼ê¸° ì •ì±… ì¶”ê°€ (ì´ë¯¸ì§€ 10ê°œë§Œ ë‚¨ê¸°ê³  ì‚­ì œ -> ë¹„ìš© ì ˆê°)
resource "aws_ecr_lifecycle_policy" "main" {
  for_each   = aws_ecr_repository.repos
  repository = each.value.name

  policy = jsonencode({
    rules = [{
      rulePriority = 1
      description  = "Keep last 10 images"
      selection    = {
        tagStatus   = "any"
        countType   = "imageCountMoreThan"
        countNumber = 10
      }
      action = { type = "expire" }
    }]
  })
}

# OIDC Providerë¥¼ "ìƒì„±"í•˜ì§€ ì•Šê³  "ì¡°íšŒ"ë§Œ í•¨ (ì¤‘ë³µ ë°©ì§€)
data "aws_iam_openid_connect_provider" "github" {
  url = "https://token.actions.githubusercontent.com"
}

# 3. GitHub Actionsìš© IAM Role
resource "aws_iam_role" "github_actions" {
  name = "${var.project_name}-${var.env}-github-actions-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRoleWithWebIdentity"
      Effect = "Allow"
      Principal = {
        # data ì†ŒìŠ¤ë¡œ ì¡°íšŒí•œ ARN ì‚¬ìš©
        Federated = data.aws_iam_openid_connect_provider.github.arn
      }
      Condition = {
        StringLike = {
          "token.actions.githubusercontent.com:sub" = [
            for repo in var.github_repos : "repo:${repo}:*"
          ]
        }
      }
    }]
  })
}

# 4. ECR Push ê¶Œí•œ ë¶€ì—¬
resource "aws_iam_role_policy" "ecr_push" {
  name = "ecr-push-policy"
  role = aws_iam_role.github_actions.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      # ë¡œê·¸ì¸ ê¶Œí•œ (Resource = "*" í•„ìˆ˜)
      {
        Effect = "Allow"
        Action = [
          "ecr:GetAuthorizationToken"
        ]
        Resource = "*"
      },
      # ë‚˜ë¨¸ì§€ ì¡°ì‘ ê¶Œí•œ (ìš°ë¦¬ê°€ ë§Œë“  Repoë§Œ í—ˆìš©)
      {
        Effect = "Allow"
        Action = [
          "ecr:BatchCheckLayerAvailability",
          "ecr:GetDownloadUrlForLayer",
          "ecr:BatchGetImage",
          "ecr:InitiateLayerUpload",
          "ecr:UploadLayerPart",
          "ecr:CompleteLayerUpload",
          "ecr:PutImage"
        ]
        # "*" ëŒ€ì‹ , ìš°ë¦¬ê°€ ë§Œë“  ë ˆí¬ì§€í† ë¦¬ë“¤ì˜ ARNë§Œ í—ˆìš©
        Resource = [for repo in aws_ecr_repository.repos : repo.arn]
      }
    ]
  })
}
